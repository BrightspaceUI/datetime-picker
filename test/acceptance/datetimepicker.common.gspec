@groups
	datetime datetimepicker,datetimepicker-labelless

== D2L Date-Time Picker ==

	@on closed
		@forEach [&datetime] as item
			${item}.time-picker, ${item}.icon-button.clear.svg:
				absent

	@on open
		@forEach [&datetime] as item
			${item}.time-picker, ${item}.icon-button.clear.svg:
				visible

			${item}.icon-button.clear.svg:
				inside ${item}

		datetimepicker.time-picker-label:
			visible

	datetimepicker.date-picker-label:
		visible

	datetimepicker-labelless.date-picker-label, datetimepicker-labelless.time-picker-label:
		absent

	@on mobile
		@on ltr
			datetimepicker.date-picker-label:
				left-of datetimepicker.d2l-date-picker.date-picker

		@on rtl
			datetimepicker.date-picker-label:
				right-of datetimepicker.d2l-date-picker.date-picker

		@on open
			@on ltr
				datetimepicker.time-picker-label:
					left-of datetimepicker.time-picker

			@on rtl
				datetimepicker.time-picker-label:
					right-of datetimepicker.time-picker

			@forEach [&datetime] as item
				${item}.time-picker:
					below ${item}.d2l-date-picker.date-picker 20px

				@on ltr
					${item}.time-picker:
						aligned vertically left ${item}.d2l-date-picker.date-picker

					${item}.icon-button.clear.svg:
						right-of ${item}.d2l-date-picker.date-picker ~34px

				@on rtl
					${item}.time-picker:
						aligned vertically right ${item}.d2l-date-picker.date-picker

					${item}.icon-button.clear.svg:
						left-of ${item}.d2l-date-picker.date-picker ~34px

	@on desktop
		@on ltr
			datetimepicker.date-picker-label:
				aligned vertically left datetimepicker.d2l-date-picker.date-picker

		@on rtl
			datetimepicker.date-picker-label:
				aligned vertically right datetimepicker.d2l-date-picker.date-picker

		@on open
			@on ltr
				datetimepicker.time-picker-label:
					aligned vertically left datetimepicker.time-picker

			@on rtl
				datetimepicker.time-picker-label:
					aligned vertically right datetimepicker.time-picker

			@forEach [&datetime] as item
				${item}.time-picker:
					aligned horizontally top ${item}.d2l-date-picker.date-picker

				@on ltr
					${item}.time-picker:
						right-of ${item}.d2l-date-picker.date-picker 20px

					${item}.icon-button.clear.svg:
						right-of ${item}.time-picker ~34px

				@on rtl
					${item}.time-picker:
						left-of ${item}.d2l-date-picker.date-picker 20px

					${item}.icon-button.clear.svg:
						left-of ${item}.time-picker ~34px
